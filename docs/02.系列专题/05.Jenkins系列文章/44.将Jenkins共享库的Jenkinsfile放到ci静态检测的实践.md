---
title: 将Jenkins共享库的Jenkinsfile放到ci静态检测的实践
titleTag: 原创
date: 2021-08-28 15:29:19
permalink: /pages/b732f5/
categories: 
  - 系列专题
  - Jenkins系列文章
tags: 
  - null

description: 
---

## 1，前言

我们所有的构建都已经托管在Jenkins上，Jenkins所有的Job都采用的pipeline形式，pipeline所有的引导文件都已经托管在gitlab统一调用共享库，共享库也都托管在gitlab像普通项目那样走开发-review的流程进行上线。

but，有一个问题是，共享库这种提纲挈领的，以一持万的，纲举目张的存在，到如今都还没有走严格意义上的开发流程。事实上前几天已经吃过这种亏了，一个贡献库文件被上百个发布job引用非常常见，而因为运维同学稍稍粗心，少了个后关闭的大括号，push到共享库之后就下班走人了，结果影响了不少项目的构建。而这种比较低级的错误，本来可以通过前置工作进行一些检测的。

## 2，调研

今天就来做一下共享库内主要发布逻辑脚本的语法静态检测，看了一下，网上有不少的相关资料，整理如下：

- [Jenkins官方文档–管道开发工具](https://www.jenkins.io/doc/book/pipeline/development/)
- stack overflow
  - [Jenkins: How do I lint Jenkins pipelines from the command line?](https://stackoverflow.com/questions/44703012/jenkins-how-do-i-lint-jenkins-pipelines-from-the-command-line)
- [jflint](https://github.com/miyajan/jflint)
  - 一个开源的检测工具，使用相当简便，本文使用这个。
- [npm-groovy-lint](https://github.com/nvuillam/npm-groovy-lint)
  - 同样是一个检测工具，看起来功能更强大，有点复杂。

## 3，准备

有了如上介绍以及文档基础之后，我们选择jflint这个工具来进行检测，结合 [jenkins作为ci检测代码是否合并的实践](https://wiki.eryajf.net/pages/615b71/#_1-gitlab%E9%85%8D%E7%BD%AE)此文章配置流水线ci，将共享库托管起来。

由于如上工具没有直接给到容器化的版本，所以我这里构建一个容器，读者可以直接拿去使用：

- 官方：`eryajf/jflint`
- 国内：`registry.cn-hangzhou.aliyuncs.com/ali_eryajf/jflint`

简单查看：

```bash
$ docker run -it  eryajf/jflint  jflint -h
Usage: jflint [options] Jenkinsfile

Options:
  -v, --version                output the version number
  -j, --jenkins-url <url>      Jenkins URL
  -u, --username <username>    Username for Jenkins
  -p, --password <password>    Password or API Token for Jenkins
  --csrf-disabled              Specify if CSRF prevention is disabled on Jenkins
  -c, --config <path>          Path to config json file
  --ssl-verification-disabled  Disable SSL verification
  -h, --help                   output usage information
```

其中在构建镜像时，声明了两个ENV，以便于调用：

```bash
ENV USER=admin
ENV PASS=admin
```

## 4，验证

此时准备一个非常简单的流水线作为测试使用：

```bash
cat >> test.jenkins << 'EOF'
pipeline {
    agent any
    environment {
        // 输出结果为 20200330142150_4
        _version = sh(script: "echo `date '+%Y%m%d%H%M%S'`" + "_${env.BUILD_ID}", returnStdout: true).trim()
    }
    stages {
        stage ("test") {
            steps {
                echo "${_version}"
            }
        }
    }
}
EOF
```

然后运行如下命令进行检测：

```bash
$ docker run -itd --name jflint eryajf/jflint # 如果你的用户名密码并不是上边两个，可以通过-e进行覆盖
$ docker cp test.jenkins jflint:/opt
$ docker exec -it jflint jflint -u $USER -p $PASS -j https://ci.test.com /opt/test.jenkins
Jenkinsfile successfully validated.
```

此时将如上流水线进行简单调整：

```groovy
cat >> test.jenkins << 'EOF'
pipeline {
    agent any
    environment {
        // 输出结果为 20200330142150_4
        _version = sh(script: "echo `date '+%Y%m%d%H%M%S'`" + "_${env.BUILD_ID}", returnStdout: true).trim()
    }
    stages {
        stage ("test") {
            steps {
                echo "${_version}"
            }
        }
    }
EOF
```

`注意删除了最后一行的括号。`

然后再次运行检测：

```bash
$ docker exec -it jflint jflint -u $USER -p $PASS -j https://ci.test.com /opt/test.jenkins
Errors encountered validating Jenkinsfile:
WorkflowScript: 14: expecting '}', found '' @ line 14, column 1.
```

可以看到不仅报错了，还提示了我们错误的行数，可以说非常清晰了。

![11](http://t.eryajf.net/imgs/2021/09/b38526bc56ae4df1.jpg)

## 5，实践

有了如上准备以及实验之后，我们就可以直接添加如下流水线，作为静态ci的配置信息：

```groovy
pipeline {
    agent any
    environment {
        // 服务名称
        SERVICE_NAME="${JOB_BASE_NAME}"
        MODE="DEPLOY"
        REASON="无"
        REMOTE_HOST="占位"
        _VERSION="测试验证"
        ROBOT_KEY="6a781aaf-0cda-41ab-9bd2-ed81ee7fc7"
        // 主项目地址
        GIT_URL = "https://gitlab.test.com/jenkins/shared-library.git"
    }
    parameters {
        string(name: 'BRANCH', defaultValue: 'master', description: '请输入将要构建的代码分支')
    }
    options {
        timestamps() // 输出构建日志打印时间信息
        timeout(time: 10, unit: 'MINUTES') // 设置构建超时时间
        buildDiscarder(logRotator(numToKeepStr: '15')) // 设置历史构建保留次数
        gitLabConnection('gitlab-token') // 操作gitlab的token
    }
    triggers{
        gitlab(
            triggerOnPush: false,
            triggerOnMergeRequest: true,
            branchFilterType: 'All',
            secretToken: "${env.GIT_TOKEN}"
        ) // 预留Gitlab提交自动构建
    }
    stages {
        stage('置为pending') {
            steps {
                script {
                    try {
                        updateGitlabCommitStatus name: 'build', state: 'pending'
                    }catch(exc) {
                        env.REASON = "置为pending出错"
                        throw(exc)
                    }
                }
            }
        }
        stage('拉取代码') {
            steps {
                script {
                    try {
                        env.CAUSE = currentBuild.getBuildCauses()[0].(userId)
                        if ("${CAUSE}" == 'null') {
                            env.BRANCH = sh(script: 'echo ${gitlabBranch}',  returnStdout: true).trim()
                        }
                        checkout(
                            [$class: 'GitSCM', doGenerateSubmoduleConfigurations: false, submoduleCfg: [], extensions: [[$class: 'CloneOption', depth: 1, noTags: false, reference: '', shallow: true]],
                            branches: [[name: "$BRANCH"]],userRemoteConfigs: [[url: "${env.GIT_URL}", credentialsId: "cicd-pass"]]]
                        )
                        // 定义全局变量
                        env.COMMIT_ID   = sh(script: 'git log --pretty=format:%h',  returnStdout: true).trim() // 提交ID
                        env.COMMIT_USER = sh(script: 'git log --pretty=format:%an', returnStdout: true).trim() // 提交者
                        env.COMMIT_TIME = sh(script: 'git log --pretty=format:%ai', returnStdout: true).trim() // 提交时间
                        env.COMMIT_INFO = sh(script: 'git log --pretty=format:%s',  returnStdout: true).trim() // 提交信息
                    }catch(exc) {
                        env.REASON = "拉取代码出错"
                        throw(exc)
                    }
                }
            }
        }
        stage('检测项目') {
            steps {
                script {
                    try {
                        docker.image('eryajf/jflint').inside("-e USER=admin -e PASS=admin12345") {
                            sh '''
                                for i in `ls vars`;do
                                    jflint -u $USER -p $PASS -j https://ci.test.com vars/$i
                                done
                            '''
                        }
                        sh "printenv"
                    }catch(exc) {
                        env.REASON = "检测项目出错"
                        throw(exc)
                    }
                }
            }
        }
    }
    post {
        always {
            script{
                wrap([$class: 'BuildUser']){
                    buildName "#${BUILD_ID}-${BRANCH}-${BUILD_USER}" // 更改构建名称
                    currentBuild.description = "提交者: ${COMMIT_USER}" // 添加说明信息
                    currentBuild.description += "\n构建主机: ${REMOTE_HOST}" // 添加说明信息
                    currentBuild.description += "\n提交ID: ${COMMIT_ID}" // 添加说明信息
                    currentBuild.description += "\n提交时间: ${COMMIT_TIME}" // 添加说明信息
                    currentBuild.description += "\n提交内容: ${COMMIT_INFO}" // 添加说明信息
                }
                sh "printenv"
            }
            cleanWs()
        }
        success {
            updateGitlabCommitStatus(name: 'build', state: 'success')
            addGitLabMRComment comment: "🤖Jenkins ci check success🥳, see the log: ${BUILD_URL}console"
        }
        failure {
            updateGitlabCommitStatus(name: 'build', state: 'failed')
            addGitLabMRComment comment: "🤖Jenkins ci check failed🤯, see the log: ${BUILD_URL}console"
        }
    }
}
```

注意，基于容器运行的时候，可以通过后边的参数来指定自己的用户名信息，当然也可以使用token的方式。

这样我们就可以把共享库的维护，走类似常规项目开发的流程，通过check普通分支，走merge的方式，并且有静态自动检测语法，成功之后方可合并到主干分支，以期将影响降到最低。